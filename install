#!/usr/bin/env zsh

cd $0:A:h

if [[ $1 == --clean ]] git clean -ffdX && rm -rfv ~/.gitidentity ~/.terminfo

# symlinks
setopt extendedglob
for f (^install) rm -rf ~/.$f && ln -sfv $PWD/$f ~/.$f

# git
for k in user.name user.email; do
  if ! git config -f ~/.gitidentity $k > /dev/null; then
    read "v?${(%):-%4FEnter default $k for git: %f}"
    if [[ -n $v ]] git config -f ~/.gitidentity $k $v
  fi
done

# terminfo
if [[ $OSTYPE == darwin* ]]; then
  tic -xo ~/.terminfo =(echo "nsterm,\n use=xterm-256color,\n\ntmux-256color,
      ritm=\\E[23m,sitm=\\E[3m,rmso=\\E[27m,smso=\\E[7m,use=screen-256color,")
fi

# vim
if ! [[ -f ~/.vim/autoload/plug.vim ]]; then
  curl -Lo ~/.vim/autoload/plug.vim \
      'https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'
  vim -c 'se ls=0 nonu cc=|au BufAdd * on' +PlugInstall +q
fi

# zsh
if ! [[ -d ~/.zsh/plugins ]] zsh -ci 'plug update'
